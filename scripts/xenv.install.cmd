@ECHO OFF

SETLOCAL
  CALL :MAIN
ENDLOCAL

EXIT /B %ERRORLEVEL%

:MAIN
  SETLOCAL ENABLEEXTENSIONS ENABLEDELAYEDEXPANSION
    CALL %~dp0xenv.base.cmd

    ECHO XENV:INSTALL
    PUSHD !_XENV!
    CALL :SECTION !_XENV!
  ENDLOCAL
GOTO :EOF

:SECTION
  SETLOCAL ENABLEEXTENSIONS ENABLEDELAYEDEXPANSION
    SET _XENV=%~1
    
    FOR /D %%S IN (*) DO (
      SET _SECTION=%%S

      IF NOT "!_SECTION:~0,1!"=="_" (
        ECHO - !_SECTION!
        PUSHD "!_XENV!\!_SECTION!"
        CALL :TOOL !_XENV! !_SECTION!
      )
    )
  ENDLOCAL
GOTO :EOF

:TOOL
  SETLOCAL ENABLEEXTENSIONS ENABLEDELAYEDEXPANSION
    SET _XENV=%~1
    SET _SECTION=%~2

    FOR /D %%T IN (*) DO (
      SET _TOOL=%%T

      IF NOT "!_TOOL:~0,1!"=="_" (
        ECHO   - !_SECTION!: !_TOOL!
        PUSHD "!_XENV!\!_SECTION!\!_TOOL!"
        CALL :VERSION !_XENV! !_SECTION! !_TOOL!
      )
    )

  ENDLOCAL
GOTO :EOF

:VERSION
  SETLOCAL ENABLEEXTENSIONS ENABLEDELAYEDEXPANSION
    SET _XENV=%~1
    SET _SECTION=%~2
    SET _TOOL=%~3

    IF EXIST %CD%\%_XD%\%_SCRIPT%\xenv.bat (
      ECHO     - installing
      START /WAIT CMD /C %CD%\%_XD%\%_SCRIPT%\xenv.bat
    )
  ENDLOCAL
GOTO :EOF
